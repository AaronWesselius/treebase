#!/usr/bin/perl

use strict;
use CIPRES::TreeBase::DBIUtil;
use Getopt::Std;

my %opt;
getopts('s', \%opt) or usage();

my $dbh = CIPRES::TreeBase::DBIUtil->dbh
  or die "Couldn't connect to database: " . DBI->errstr;

@ARGV || usage();
for my $matrix_id (@ARGV) {
    show_matrix($matrix_id);
}


sub show_matrix {
    my $mid = shift;
    {
	my $q = "select title, nexusfilename from matrix where matrix_id = ?";
	my $sth = $dbh->prepare_cached($q);
	$sth->execute($mid);
	my ($title, $nexus) = $sth->fetchrow;
	print "MATRIX $mid '$title' ($nexus)\n";
	$sth->finish;
    }
    return if $opt{s};

    my @rows =  do {
	my $q = "SELECT matrixrow_id from matrixrow where matrix_id = ?";
	@{$dbh->selectcol_arrayref($q, {}, $mid)};
    };
    show_matrixrow($_) for @rows;
}

sub show_matrixrow {
    my $row = shift;

    {	
	my $q = "SELECT value from matrixelement where matrixrow_id = ? order by element_order";
	my $sth = $dbh->prepare_cached($q);
	$sth->execute($row);
	my $val;
	$sth->bind_col(1, \$val);
	print "$row> ";
	while ($sth->fetch) {
	    print defined($val) ? $val : '-';
	}
	$sth->finish;
    }

    {
	my $q = "SELECT tl.taxonlabel_id, tl.taxonlabel from matrixrow mr, taxonlabel tl where mr.taxonlabel_id = tl.taxonlabel_id and mr.matrixrow_id = ?";
	my $sth = $dbh->prepare_cached($q);
	$sth->execute($row);
	my ($id, $label) = $sth->fetchrow_array();

	print "    $label ($id)";
	$sth->finish;
    }

    print "\n";
}



sub usage {
    print "$0 [-s] matrix-id...\n";
    print "  -s : suppress matrix data\n";
    exit 1;
}

