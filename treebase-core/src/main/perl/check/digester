#!/usr/bin/perl
#
# Digest reports that come out of the checker:
#   grep out all the lines that begin with '***'
#   pass them through this program
#   This program will sort each different kind of message into a separate file
#   To have the files placed in some other directory, use the -d option
#   20090327 MJD
#

use Getopt::Std;
my %opt = (d => ".");
getopts('d:', \%opt) or die;
-d($opt{d}) || mkdir($opt{d}) || die "$opt{d}: $!";

while (<>) {
  chomp;
  my $k = $_;
  $k =~ s/\d+/#/g;
  $k =~ s/#(\s*#)+/##/g;
  $k =~ s/^S#:\s+//;
  push @{$msg{$k}}, $_;
}

for my $k (keys %msg) {
  my $f = abbr($k);
  open my($fh), ">", "$opt{d}/$f" or die "$opt{d}/$f: $!";
  print $fh $_, "\n" for @{$msg{$k}};
  close $fh;
}

my $q;
BEGIN { $q = 1 };
my %seen;

sub abbr {
  my $s = shift;
  $s =~ tr/A-Z /a-z_/;
  $s =~ tr/A-Za-z_//cd;
  $s =~ tr/_/_/s;
  $s =~ s/^_//;

  substr($s, 30) = "" if length($s) > 30;
  if ($seen{$s}) {
      $s .= $q++;
  }
  return $s;
}

