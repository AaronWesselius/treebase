#!/usr/bin/perl

#
# Monitor java processes and kill them when they get too old
# This will also kill your eclipse, so don't run it on your 
# desk workstation.
#
# Author: mjd 20090206

use Fcntl ':flock';

# Only one instance of this program should run at a time
open F, "+<", $0 or die "open $0: $!";
flock F, LOCK_EX | LOCK_NB
  or exit 0;

while (1) {
  my @pslines = qx{/bin/ps -lf --User mjd};
  die "Couldn't run ps!" if @pslines == 0 || $? != 0;
  shift @pslines;		# Discard header line
  for (@pslines) {
    my ($uid, $pid, $ppid, $c, $stime, $tty, $time, $cmd) = 
      split /\s+/, $_, 8;
    next unless $cmd =~ /^java\s/;
    my $age = age($stime);
    if ($age > 15) {
      warn "$pid ($cmd) is $age minutes old; killing\n";
      kill 15 => $pid or warn "  kill: $!";
    }
  }
  sleep 60;
}

# Return amount of time since process was started, in minutes
sub age {
  my $stime = shift;

  # Could be "11:23" or "Feb05"
  # If no colon, it's really old; don't bother to calculate how old
  return 10000000 unless my ($h, $m) = split /:/, $stime;

  my ($sc, $mn, $hr) = localtime();
  return ($hr-$h)*60 + $mn-$m;
}

