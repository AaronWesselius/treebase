#!/usr/bin/perl

use strict;
use CIPRES::TreeBase::DBIUtil;
use Getopt::Std;

my %opt = (s => 'JRUAN');
getopts('hs:', \%opt) or usage();
if ($opt{h}) { die "Sorry, unimplemented.\n"; }

my $dbh = CIPRES::TreeBase::DBIUtil->dbh
  or die "Couldn't connect to database: " . DBI->errstr;

# Default: Process every table 'X' that has an 'X_ID' column
unless (@ARGV) {
    my $sth = $dbh->prepare(
"select tbname from sysibm.syscolumns where tbcreator = ? and name = concat(tbname, '_ID')",
);
    $sth->execute($opt{s});
    while (my ($table) = $sth->fetchrow_array) {
      push @ARGV, $table;
    }
}

for my $table (@ARGV) {
    my $column = id_column($table) or next;
    my $next_id = quick_select($dbh,
	"select 1+max($column) from $table");
    next unless defined $next_id;
    warn "Adjusting next ID of $table.$column to $next_id\n";
    $dbh->do("alter table $table alter column $column restart with $next_id");
}

{
    my %exception;
    BEGIN {
	%exception = 
	    # Undef means that the table has no autoincrementing
	    # ID column.  If the table does have such a column, its name is
	    # here, unless the name is the just "TABLE_ID".
	    #
            # WARNING: Table is very incomplete
	    (
	     chargroup_colrange => undef,
	     charset_colrange => undef,
	     charweight_colrange => undef,
	     codonchar1_colrange => undef,
	     codonchar2_colrange => undef,
	     codonchar3_colrange => undef,
	     leftchangeset_colrange => undef,
	     rightchangeset_colrange => undef,
	     leftchangeset_charstate => undef,
	     rightchangeset_charstate => undef,
	    );
    }

    sub id_column {
	my $table = shift;
	return exists($exception{lc $table}) 
	    ? $exception{lc $table} 
	    : "$table\_id";
    }
}

